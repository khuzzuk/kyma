plugins {
    id 'idea'
    id 'java'
    id 'groovy'
    id 'maven'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id "org.beryx.jlink" version "2.4.4"
}

group = 'kyma.net'
version = '1.0-SNAPSHOT'

description = "Kyma music player and manager"

sourceCompatibility = 11
targetCompatibility = 11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {

    maven { url "http://repo1.maven.org/maven2" }
    maven { url "https://raw.github.com/khuzzuk/MessageBus/mvn-repo/" }
    maven { url "https://raw.github.com/khuzzuk/functional-logic/mvn-repo/" }
    flatDir {
        dirs 'lib'
    }
}

javafx {
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.fxml']
}

ext {
    luceneVersion = '7.6.0'
    commonsLangVersion = '3.7'
    commonsCollectionsVersion = '4.1'
    jacksonVersion = '2.9.4'
    log4jVersion = '2.11.0'
    lombokVersion = '1.18.6'
}

dependencies {
    compile group: 'org.apache.lucene', name: 'lucene-core', version: luceneVersion
    compile group: 'org.apache.commons', name: 'commons-lang3', version: commonsLangVersion
    compile group: 'org.apache.commons', name: 'commons-collections4', version: commonsCollectionsVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: log4jVersion
    compile group: 'com.googlecode.soundlibs', name: 'jlayer', version: '1.0.1.4'
    compile group: 'com.googlecode.soundlibs', name: 'tritonus-share', version: '0.3.7.4'
    compile group: 'com.googlecode.soundlibs', name: 'mp3spi', version: '1.9.5.4'
    compile group: 'com.github.trilarion', name: 'vorbis-support', version: '1.1.0'
    compile name: 'jaudiotagger-2.2.6-SNAPSHOT'
    compile name: 'jflac-1.3'
    compile name: 'jaad-0.8.6f'
    compile group: 'pl.khuzzuk', name: 'message-bus', version: '2.0.1'
    compile group: 'pl.khuzzuk', name: 'functional-logic', version: '0.2.0'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.13'
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testCompile group: 'org.awaitility', name: 'awaitility', version: '3.1.0'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

mainClassName = 'kyma/net.kyma.Manager'

compileJava {
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--patch-module', "$moduleName=" + files(sourceSets.main.resources.srcDirs).asPath,
                //'--module-version', "$moduleVersion"
        ]
    }
}

run {
    inputs.property('moduleName', moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module-version', "$mainClassName"
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes 'Main-class': 'net.kyma.Manager'
    }
//    from {
//        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
//    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'kyma'
    }
    forceMerge('log4j-api')
}
